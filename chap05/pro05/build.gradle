plugins {
  id 'java'
  id "io.freefair.lombok" version "${lombokPluginVersion}"
  id "org.gretty" version "${grettyVersion}"
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of("${javaVersion}")
  }
}

compileJava.options.encoding = "${projectEncoding}"
compileTestJava.options.encoding = "${projectEncoding}"

repositories {
  mavenCentral()
}

dependencies {
  implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
  implementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4jVersion}"

  compileOnly "jakarta.servlet:jakarta.servlet-api:${jakartaServletApiVersion}"

  testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
  testImplementation "org.assertj:assertj-core:${assertJVersion}"

  testRuntimeOnly "jakarta.servlet:jakarta.servlet-api:${jakartaServletApiVersion}"

  // Mockito를 사용할까? 하다가.. Spring Test에서 지원해주는 내용이 좋아서 이것을 추가해서 사용하기로 했다.
  testImplementation "org.springframework:spring-test:${springVersion}"
  testImplementation "org.springframework:spring-web:${springVersion}"
}

// gradle appRun 으로 실행
gretty {
  httpPort = 8090
  contextPath = project.name
  servletContainer = "tomcat10"
  inplaceMode = "hard"
  reloadOnClassChange = true
  loggingLevel= "DEBUG"

  jvmArgs = [
      "--add-opens=java.base/java.io=ALL-UNNAMED",
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "--add-opens=java.rmi/sun.rmi.transport=ALL-UNNAMED"
  ]
}

test {
  useJUnitPlatform()
  testLogging {
    outputs.upToDateWhen { false }
    showStandardStreams = true
  }
}
